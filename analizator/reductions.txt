<primarni_izraz> IDN
<primarni_izraz> BROJ
<primarni_izraz> ZNAK
<primarni_izraz> NIZ_ZNAKOVA
<primarni_izraz> L_ZAGRADA <izraz> D_ZAGRADA
<postfiks_izraz> <primarni_izraz>
<postfiks_izraz> <postfiks_izraz> L_UGL_ZAGRADA <izraz> D_UGL_ZAGRADA
<postfiks_izraz> <postfiks_izraz> L_ZAGRADA D_ZAGRADA
<postfiks_izraz> <postfiks_izraz> L_ZAGRADA <lista_argumenata> D_ZAGRADA
<postfiks_izraz> <postfiks_izraz> TOCKA IDN
<postfiks_izraz> <postfiks_izraz> OP_INC
<postfiks_izraz> <postfiks_izraz> OP_DEC
<lista_argumenata> <izraz_pridruzivanja>
<lista_argumenata> <lista_argumenata> ZAREZ <izraz_pridruzivanja>
<unarni_izraz> <postfiks_izraz>
<unarni_izraz> OP_INC <unarni_izraz>
<unarni_izraz> OP_DEC <unarni_izraz>
<unarni_izraz> <unarni_operator> <cast_izraz>
<unarni_operator> AMPERSAND
<unarni_operator> ASTERISK
<unarni_operator> PLUS
<unarni_operator> MINUS
<unarni_operator> OP_TILDA
<unarni_operator> OP_NEG
<cast_izraz> <unarni_izraz>
<cast_izraz> L_ZAGRADA <ime_tipa> D_ZAGRADA <cast_izraz>
<multiplikativni_izraz> <cast_izraz>
<multiplikativni_izraz> <multiplikativni_izraz> ASTERISK <cast_izraz>
<multiplikativni_izraz> <multiplikativni_izraz> OP_DIJELI <cast_izraz>
<multiplikativni_izraz> <multiplikativni_izraz> OP_MOD <cast_izraz>
<aditivni_izraz> <multiplikativni_izraz>
<aditivni_izraz> <aditivni_izraz> PLUS <multiplikativni_izraz>
<aditivni_izraz> <aditivni_izraz> MINUS <multiplikativni_izraz>
<odnosni_izraz> <aditivni_izraz>
<odnosni_izraz> <odnosni_izraz> OP_LT <aditivni_izraz>
<odnosni_izraz> <odnosni_izraz> OP_GT <aditivni_izraz>
<odnosni_izraz> <odnosni_izraz> OP_LTE <aditivni_izraz>
<odnosni_izraz> <odnosni_izraz> OP_GTE <aditivni_izraz>
<jednakosni_izraz> <odnosni_izraz>
<jednakosni_izraz> <jednakosni_izraz> OP_EQ <odnosni_izraz>
<jednakosni_izraz> <jednakosni_izraz> OP_NEQ <odnosni_izraz>
<bin_i_izraz> <jednakosni_izraz>
<bin_i_izraz> <bin_i_izraz> AMPERSAND <jednakosni_izraz>
<bin_xili_izraz> <bin_i_izraz>
<bin_xili_izraz> <bin_xili_izraz> OP_BIN_XILI <bin_i_izraz>
<bin_ili_izraz> <bin_xili_izraz>
<bin_ili_izraz> <bin_ili_izraz> OP_BIN_ILI <bin_xili_izraz>
<log_i_izraz> <bin_ili_izraz>
<log_i_izraz> <log_i_izraz> OP_I <bin_ili_izraz>
<log_ili_izraz> <log_i_izraz>
<log_ili_izraz> <log_ili_izraz> OP_ILI <log_i_izraz>
<izraz_pridruzivanja> <log_ili_izraz>
<izraz_pridruzivanja> <unarni_izraz> OP_PRIDRUZI <izraz_pridruzivanja>
<izraz> <izraz_pridruzivanja>
<izraz> <izraz> ZAREZ <izraz_pridruzivanja>
<deklaracija> <specifikatori_deklaracije> TOCKAZAREZ
<deklaracija> <specifikatori_deklaracije> <lista_init_deklaratora> TOCKAZAREZ
<specifikatori_deklaracije> <specifikator_tipa>
<specifikatori_deklaracije> <specifikator_tipa> <specifikatori_deklaracije>
<specifikatori_deklaracije> KR_CONST
<specifikatori_deklaracije> KR_CONST <specifikatori_deklaracije>
<lista_init_deklaratora> <init_deklarator>
<lista_init_deklaratora> <lista_init_deklaratora> ZAREZ <init_deklarator>
<init_deklarator> <deklarator>
<init_deklarator> <deklarator> OP_PRIDRUZI <inicijalizator>
<specifikator_tipa> KR_VOID
<specifikator_tipa> KR_CHAR
<specifikator_tipa> KR_INT
<specifikator_tipa> KR_FLOAT
<specifikator_tipa> <struct_specifikator>
<struct_specifikator> KR_STRUCT IDN L_VIT_ZAGRADA <struct_lista_deklaracija> D_VIT_ZAGRADA
<struct_specifikator> KR_STRUCT L_VIT_ZAGRADA <struct_lista_deklaracija> D_VIT_ZAGRADA
<struct_specifikator> KR_STRUCT IDN
<struct_lista_deklaracija> <struct_deklaracija>
<struct_lista_deklaracija> <struct_lista_deklaracija> <struct_deklaracija>
<struct_deklaracija> <lista_specifikatora_kvalifikatora> <struct_lista_deklaratora> TOCKAZAREZ
<lista_specifikatora_kvalifikatora> <specifikator_tipa> <lista_specifikatora_kvalifikatora>
<lista_specifikatora_kvalifikatora> <specifikator_tipa>
<lista_specifikatora_kvalifikatora> KR_CONST <lista_specifikatora_kvalifikatora>
<lista_specifikatora_kvalifikatora> KR_CONST
<struct_lista_deklaratora> <struct_deklarator>
<struct_lista_deklaratora> <struct_lista_deklaratora> ZAREZ <struct_deklarator>
<struct_deklarator> <deklarator>
<deklarator> <pokazivac> <izravni_deklarator>
<deklarator> <izravni_deklarator>
<izravni_deklarator> IDN
<izravni_deklarator> <izravni_deklarator> L_UGL_ZAGRADA <log_ili_izraz> D_UGL_ZAGRADA
<izravni_deklarator> <izravni_deklarator> L_UGL_ZAGRADA D_UGL_ZAGRADA
<izravni_deklarator> <izravni_deklarator> L_ZAGRADA <lista_parametara> D_ZAGRADA
<pokazivac> ASTERISK
<pokazivac> ASTERISK KR_CONST
<lista_parametara> <deklaracija_parametra>
<lista_parametara> <lista_parametara> ZAREZ <deklaracija_parametra>
<deklaracija_parametra> <specifikatori_deklaracije> <deklarator>
<deklaracija_parametra> <specifikatori_deklaracije>
<ime_tipa> <lista_specifikatora_kvalifikatora>
<ime_tipa> <lista_specifikatora_kvalifikatora> <pokazivac>
<inicijalizator> <izraz_pridruzivanja>
<inicijalizator> L_VIT_ZAGRADA <lista_izraza_pridruzivanja> D_VIT_ZAGRADA
<inicijalizator> L_VIT_ZAGRADA <lista_izraza_pridruzivanja> ZAREZ D_VIT_ZAGRADA
<lista_izraza_pridruzivanja> <izraz_pridruzivanja>
<lista_izraza_pridruzivanja> <lista_izraza_pridruzivanja> ZAREZ <izraz_pridruzivanja>
<naredba> <slozena_naredba>
<naredba> <izraz_naredba>
<naredba> <naredba_grananja>
<naredba> <naredba_petlje>
<naredba> <naredba_skoka>
<slozena_naredba> L_VIT_ZAGRADA D_VIT_ZAGRADA
<slozena_naredba> L_VIT_ZAGRADA <lista_naredbi> D_VIT_ZAGRADA
<slozena_naredba> L_VIT_ZAGRADA <lista_deklaracija> D_VIT_ZAGRADA
<slozena_naredba> L_VIT_ZAGRADA <lista_deklaracija> <lista_naredbi> D_VIT_ZAGRADA
<lista_deklaracija> <deklaracija>
<lista_deklaracija> <lista_deklaracija> <deklaracija>
<lista_naredbi> <naredba>
<lista_naredbi> <lista_naredbi> <naredba>
<izraz_naredba> TOCKAZAREZ
<izraz_naredba> <izraz> TOCKAZAREZ
<naredba_grananja> KR_IF L_ZAGRADA <izraz> D_ZAGRADA <naredba>
<naredba_grananja> KR_IF L_ZAGRADA <izraz> D_ZAGRADA <naredba> KR_ELSE <naredba>
<naredba_petlje> KR_WHILE L_ZAGRADA <izraz> D_ZAGRADA <naredba>
<naredba_petlje> KR_FOR L_ZAGRADA <izraz_naredba> <izraz_naredba> D_ZAGRADA <naredba>
<naredba_petlje> KR_FOR L_ZAGRADA <izraz_naredba> <izraz_naredba> <izraz> D_ZAGRADA <naredba>
<naredba_skoka> KR_CONTINUE TOCKAZAREZ
<naredba_skoka> KR_BREAK TOCKAZAREZ
<naredba_skoka> KR_RETURN TOCKAZAREZ
<naredba_skoka> KR_RETURN <izraz> TOCKAZAREZ
<prijevodna_jedinica> <vanjska_deklaracija>
<prijevodna_jedinica> <prijevodna_jedinica> <vanjska_deklaracija>
<vanjska_deklaracija> <definicija_funkcije>
<vanjska_deklaracija> <deklaracija>
<definicija_funkcije> <specifikatori_deklaracije> <deklarator> <lista_deklaracija> <slozena_naredba>
<definicija_funkcije> <specifikatori_deklaracije> <deklarator> <slozena_naredba>
<definicija_funkcije> <deklarator> <lista_deklaracija> <slozena_naredba>
<definicija_funkcije> <deklarator> <slozena_naredba>
